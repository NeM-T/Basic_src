! 統計ライブラリ2次元配列用
!
! 平均
EXTERNAL FUNCTION mean(x(,),n)
! 第n列の平均  
DECLARE NUMERIC k,t
LET t=0
FOR k=LBOUND(x,1) TO UBOUND(x,1)
   LET t=t+x(k,n)
NEXT k
LET mean=t/SIZE(x,1)
END FUNCTION
! 
! 共分散
EXTERNAL FUNCTION covariance(x(,),i,j)
! i列とj列の共分散
DECLARE EXTERNAL FUNCTION mean
DECLARE NUMERIC mi,mj,k,s
LET mi=mean(x,i)
LET mj=mean(x,j)
LET s=0
FOR k=LBOUND(x,1) TO UBOUND(x,1)
   LET s=s+(x(k,i)-mi)*(x(k,j)-mj)
NEXT k
LET covariance=s/SIZE(x,1)
END FUNCTION
! 
! 分散
EXTERNAL FUNCTION variance(x(,),n)
! 第n列の分散
DECLARE EXTERNAL FUNCTION covariance
LET variance=covariance(x,n,n)
END FUNCTION
!
! 標準偏差
EXTERNAL FUNCTION sd(x(,),n)
! 第n列の標準偏差
DECLARE EXTERNAL FUNCTION variance
LET sd=SQR(variance(x,n))
END FUNCTION
!
! 相関係数
EXTERNAL FUNCTION correlation(x(,),i,j)
! i列とj列の相関係数
DECLARE EXTERNAL FUNCTION covariance,sd
LET correlation=covariance(x,i,j)/(sd(x,i)*sd(x,j))
END FUNCTION
!
! 散布図を描く
EXTERNAL SUB scatter(x(,),i,j)
! 座標系は呼び出し元で設定しておく
DECLARE NUMERIC k
FOR k=LBOUND(x,1) TO UBOUND(x,1)
   PLOT POINTS: x(k,i),x(k,j)
NEXT k
END SUB
!
! 散布図と回帰直線を描く
EXTERNAL SUB chart(x(,),i,j)
! 座標系は呼び出し元で設定しておく
DECLARE EXTERNAL SUB scatter
DECLARE EXTERNAL FUNCTION mean,variance,correlation
DECLARE NUMERIC mx,my,sx,sy,r
CALL scatter(x,i,j)
LET mx=mean(x,i)
LET my=mean(x,j)
LET sx=variance(x,i)
LET sy=variance(x,j)
LET r=correlation(x,i,j) 
DECLARE NUMERIC left,right,top,bottom
ASK WINDOW left,right,bottom,top
DEF f(x)=r*sy/sx*(x-mx)+my
PLOT LINES:left,f(left);right,f(right)
END SUB
