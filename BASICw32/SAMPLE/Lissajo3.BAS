100 REM リサージュ模様の射影変換
110 REM 実行に時間がかかります。
120 DECLARE EXTERNAL PICTURE lissa
130 OPTION ARITHMETIC NATIVE
140 DIM T(4,4)
150 MAT READ T
160 DATA 1, 0, 0, 0.1
170 DATA 0, 1, 0,-0.1
180 DATA 0, 0, 1, 0
190 DATA 2, 2, 0, 1 
200 SET WINDOW -80, 80, -80, 80  
210 FOR a=1 TO 14
220    WAIT DELAY 1
230    CLEAR
240    DRAW axes(10,10)
250    SET AREA COLOR a
260    DRAW lissa WITH SCALE(a)*T
270 NEXT a
280 END                                                        
290 EXTERNAL PICTURE lissa
300 OPTION ARITHMETIC NATIVE
310 OPTION ANGLE DEGREES
320 DIM M(0 to 720,2)
330 FOR t=0 TO 359
340    LET M(t,1)=SIN(3*t)
350    LET M(t,2)=COS(7*t+15)
360 NEXT t
370 MAT PLOT AREA,LIMIT 360:M
380 END PICTURE
