100 ! シルピンスキーのガスケット
110 ! 正三角形ABCの各頂点を中心とする1/2倍の縮小写像をそれぞれ，f1,f2,f3とします。
120 ! f1,f2,f3に対して，f1(D)∪f2(D)∪f3(D)=D となる自己相似図形をシルピンスキーのガスケットといいます。
130 ! このプログラムは，上の定義をほとんどそのままに実行してシルピンスキーのガスケットの近似解を描きます。
140 ! このプログラムでは，３頂点の座標をA(0,0)，B(2,0)，C(1,SQR(3)) と仮定します。
150 ! 近似の精度は210行のIF文の  n=＿   に指定する定数で決定されます。
160 ! これは，再帰の深さです。
170 ! 今考えている写像は1/2倍の倍率を持ちますから，10回の再帰で大きさが1/1024になります。
180 ! 1/1024という大きさは，ディスプレーの解像度（縦横のドット数）と比較して小さい値なので，そこでは求める図形を1点で近似できます。
190 ! したがって，描画領域のドット数を増やして実行する場合には，210行の定数を大きくして実行する必要があります。
200 PICTURE gasket(n)
210    IF n=12 THEN 
220       PLOT POINTS: 0,0
230    ELSE
240       DRAW gasket(n+1) WITH SCALE(1/2) 
250       DRAW gasket(n+1) WITH SHIFT(-2,0)*SCALE(1/2)*SHIFT(2,0)
260       DRAW gasket(n+1) WITH SHIFT(-1,-SQR(3))*SCALE(1/2)*SHIFT(1,SQR(3))
270    END IF
280 END PICTURE
290 SET WINDOW 0,2,0,2
300 SET POINT STYLE 1
310 DRAW gasket(0)
320 END